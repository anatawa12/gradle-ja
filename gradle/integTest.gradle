apply plugin: 'java'

sourceSets {
    integTest {
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
}

configurations {
    integTestCompile.extendsFrom testCompile
    integTestRuntime.extendsFrom testRuntime
}

dependencies {
    integTestCompile project(":internalIntegTesting")

    //so that implicit help tasks are available:
    integTestRuntime project(':diagnostics')
    //above can be removed when we implement the auto-apply plugins
}

ext.integTestTasks = tasks.withType(Test).matching { it.name.toLowerCase().endsWith('integtest') }

// TODO:ADAM - remove this
task cleanUpCaches {
    doLast {
        File intTestHomeDir = rootProject.file("intTestHomeDir")

        // Clean up files for previous versions
        File previousVersionsDir = new File(intTestHomeDir, "previousVersion")
        if (previousVersionsDir.directory) {
            for (File versionDir: previousVersionsDir.listFiles()) {
                // Clean up the old lock files
                File lockFile = new File(versionDir, "cache.properties.lock")
                if (lockFile.file && lockFile.length() > 0) {
                    def version = lockFile.bytes[0]
                    if (version != 1) {
                        println "Cleaning lock file for ${versionDir}"
                        lockFile.delete()
                    }
                }
                // Clean up exploded distribution from old location
                File oldDistDir = new File(versionDir, "cache-lock-5")
                if (oldDistDir.directory) {
                    println "Cleaning unused previous distribution from ${oldDistDir}"
                    delete(oldDistDir)
                }
            }
        }

        // Clean up the downloaded distribution from old location
        if (intTestHomeDir.directory) {
            for (File child: intTestHomeDir.listFiles()) {
                if (child.file && child.name.matches("gradle-.*-bin.zip")) {
                    println "Cleaning unused previous distribution ${child}"
                    delete(child)
                }
            }
        }

        // Clean up the lock files for the shared cache
        File modulesCacheDir = rootProject.file("intTestHomeDir/worker-1/caches/modules-1")
        if (modulesCacheDir.directory) {
            File lockFile = new File(modulesCacheDir, "modules-1.lock")
            if (lockFile.file && lockFile.length() > 0) {
                def version = lockFile.bytes[0]
                if (version != 3) {
                    println "Cleaning lock file for ${modulesCacheDir}"
                    lockFile.delete()
                }
            }
        }
    }
}

integTestTasks.all { Test task ->
    dependsOn ':intTestImage', 'cleanUpCaches'
    testClassesDir = sourceSets.integTest.output.classesDir
    classpath = sourceSets.integTest.runtimeClasspath
    testSrcDirs = []
    jvmArgs '-Xmx512m', '-XX:MaxPermSize=256m', '-XX:+HeapDumpOnOutOfMemoryError'

    reports.junitXml.destination = file("${project.testResultsDir}/$name")

    systemProperties['org.gradle.integtest.versions'] = project.hasProperty("testAllVersions") ? 'all' : 'latest'
    if (project.hasProperty('crossVersionTestsOnly')) {
        include '**/*CrossVersion*'
        forkEvery = 1
    }

    systemProperties['org.gradle.integtest.cpp.toolChains'] = project.hasProperty("testAllPlatforms") ? 'all' : 'default';

    dependsOn project.task("configure${task.name.capitalize()}") << {
        configure(task) {
            reports.html.destination = file("${project.reporting.baseDir}/$name")
            systemProperties['integTest.gradleHomeDir'] = rootProject.intTestImage.destinationDir.absolutePath
            systemProperties['integTest.gradleUserHomeDir'] = rootProject.file('intTestHomeDir').absolutePath
            systemProperties['integTest.libsRepo'] = rootProject.file('build/repo')
            
            // If the projects int test need the distributions, they should add:
            // inputs.files rootProject.buildDists
            systemProperties['integTest.distsDir'] = rootProject.distsDir.absolutePath

            // The user home dir is not wiped out by clean
            // Move the daemon working space underneath the build dir so they don't pile up on CI
            systemProperties['org.gradle.integtest.daemon.registry'] = file("$rootProject.buildDir/daemon").absolutePath
        }
    }
}

task integTest(type: Test) {
    def defaultExecuter = project.hasProperty("defaultIntegTestExecuter") ? project.defaultIntegTestExecuter : "embedded"
    systemProperties['org.gradle.integtest.executer'] = defaultExecuter
}
check.dependsOn(integTest)

['embedded', 'forking', 'daemon', 'embeddedDaemon', 'parallel'].each { mode ->
    def taskName = "${mode}IntegTest"
    tasks.create(taskName, Test).configure {
        systemProperties['org.gradle.integtest.executer'] = mode
    }
}

plugins.withType(org.gradle.plugins.ide.idea.IdeaPlugin) { // lazy as plugin not applied yet
    idea {
        module {
            testSourceDirs += sourceSets.integTest.groovy.srcDirs
            testSourceDirs += sourceSets.integTest.resources.srcDirs
            scopes.TEST.plus.add(configurations.integTestCompile)
            scopes.TEST.plus.add(configurations.integTestRuntime)
        }
    }
}

plugins.withType(org.gradle.plugins.ide.eclipse.EclipsePlugin) { // lazy as plugin not applied yet
    eclipse {
        classpath {
            plusConfigurations.add(configurations.integTestCompile)
            plusConfigurations.add(configurations.integTestRuntime)
        }
    }
}
